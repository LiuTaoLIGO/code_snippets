# modulators for core interferometer sensing - Advanced LIGO, CQG, 2015
# http://iopscience.iop.org/article/10.1088/0264-9381/32/7/074001/meta#cqg507871s4-8
# 9MHz (CARM, PRC, SRC loops)
variable f1 9099471
variable f2 (5*&f1)
variable nsilica 1.44963098985906
variable Mloss 30u
###############################################################################
###   length definitions
###############################################################################
variable Larm 3994
variable LPR23 16.164  # distance between PR2 and PR3
variable LSR23 15.443  # distance between SR2 and SR3
variable LPR3BS 19.538 # distance between PR3 and BS
variable LSR3BS 19.366 # distance between SR3 and BS
variable lmich 5.342   # average length of MICH
variable lschnupp 0.08
variable lPRC ((3.5*299792458.0)/(2*&f1)) # T1000298 Eq2.1, N=3
variable lSRC ((17*299792458.0)/(2*&f2)) # T1000298 Eq2.2, M=3
###############################################################################
###   laser
###############################################################################
laser L0 P=125
mod mod1 f=&f1 midx=0.18 order=1 mod_type=pm
mod mod2 f=&f2 midx=0.18 order=1 mod_type=pm

###############################################################################
###   PRC
###############################################################################
s sPRCin mod2.p2 PRMAR.p1
m PRMAR R=0 T=0.99996 L=40u   phi=&PRM.phi xbeta=&PRM.xbeta ybeta=&PRM.ybeta
s sPRMsub1 PRMAR.p2 PRM.p1 L=0.0737 nr=&nsilica
m PRM R=0.9699915 T=0.03 L=8.5u Rc=[11.009, 11.009]
s lp1 PRM.p2 PR2.p1 L=(((&lPRC-&LPR3BS)-&LPR23)-&lmich)
bs PR2 R=(1-(0.00025+&Mloss)) T=250u L=&Mloss alpha=-0.79 Rc=[-4.545, -4.545]
s lp2 PR2.p2 PR3.p1 L=&LPR23
bs PR3 R=(1-(0.0+&Mloss)) T=0 L=&Mloss alpha=0.615 Rc=[36.027, 36.027]
s lp3 PR3.p2 BS.p1 L=&LPR3BS
###############################################################################
###   BS
###############################################################################
bs BS R=0.5 T=(1-(0.5+&Mloss)) L=&Mloss alpha=45
s BSsub1 BS.p3 BSAR1.p1 L=0.0687 nr=&nsilica
s BSsub2 BS.p4 BSAR2.p2 L=0.0687 nr=&nsilica
bs BSAR1  R=0 T=0.99995 L=50u alpha=-29.195
bs BSAR2  R=0 T=0.99995 L=50u alpha=29.195
###############################################################################
###   Yarm
###############################################################################
# Distance from beam splitter to Y arm input mirror
s ly1 BS.p2 ITMYlens.p1 L=((&lmich-(&lschnupp/2))-(&ITMYsub.L*&ITMXsub.nr))
var lensfY 34500
lens ITMYlens f=&lensfY
s ly2 ITMYlens.p2 ITMYAR.p1
m ITMYAR R=0 T=0.99998 L=20u   phi=&ITMY.phi xbeta=&ITMY.xbeta ybeta=&ITMY.ybeta
s ITMYsub ITMYAR.p2 ITMY.p1 L=0.2 nr=&nsilica
m ITMY R=(1-(0.014+&Mloss)) T=0.014 L=&Mloss Rc=[-1934.0, -1934.0]
s LY ITMY.p2 ETMY.p1 L=&Larm
m ETMY R=(1-(5e-06+&Mloss)) T=5u L=&Mloss Rc=[2245.0, 2245.0]
s ETMYsub ETMY.p2 ETMYAR.p1 L=0.2 nr=&nsilica
m ETMYAR 0 500u L=0.9995   phi=&ETMY.phi xbeta=&ETMY.xbeta ybeta=&ETMY.ybeta
###############################################################################
###   Xarm
###############################################################################
# Distance from beam splitter to X arm input mirror
s lx1 BSAR1.p3 ITMXlens.p1 L=(((&lmich+(&lschnupp/2))-(&ITMXsub.L*&ITMXsub.nr))-(&BSsub1.L*&BSsub1.nr))
var lensfX 34500
lens ITMXlens f=&lensfX
s lx2 ITMXlens.p2 ITMXAR.p1
m ITMXAR R=0 T=0.99998 L=20u   phi=&ITMX.phi xbeta=&ITMX.xbeta ybeta=&ITMX.ybeta
s ITMXsub ITMXAR.p2 ITMX.p1 L=0.2 nr=&nsilica
m ITMX R=(1-(0.014+&Mloss)) T=0.014 L=&Mloss Rc=[-1934.0, -1934.0]
s LX ITMX.p2 ETMX.p1 L=&Larm
m ETMX R=(1-(5e-06+&Mloss)) T=5u L=&Mloss Rc=[2245.0, 2245.0]
s ETMXsub ETMX.p2 ETMXAR.p1 L=0.2 nr=&nsilica
m ETMXAR 0 500u L=0.9995   phi=&ETMX.phi xbeta=&ETMX.xbeta ybeta=&ETMX.ybeta
###############################################################################
###   SRC
###############################################################################
s ls3 BSAR2.p4 SR3.p1 L=&LSR3BS
bs SR3 R=(1-(0.0+&Mloss)) T=0 L=&Mloss alpha=0.785 Rc=[35.972841, 35.972841]
s ls2 SR3.p2 SR2.p1 L=&LSR23
bs SR2 R=(1-(0.0+&Mloss)) T=0 L=&Mloss alpha=-0.87 Rc=[-6.406, -6.406]
s ls1 SR2.p2 SRM.p1 L=((((&lSRC-&LSR3BS)-&LSR23)-(&BSsub2.L*&BSsub2.nr))-&lmich)
m SRM R=0.7999913 T=0.2 L=8.7u Rc=[-5.6938, -5.6938]
s SRMsub SRM.p2 SRMAR.p1 L=0.0749 nr=&nsilica
m SRMAR R=0 T=0.99999995 L=50n
###############################################################################
###   OMC
###############################################################################
# Here we just use some simple filter to approximate an OMC for filtering
# out RF fields, this doesn't filter HOMs!
dbs OFI
#sq sqz db=3 angle=90.0
#obp OMC fc=0 bandwidth=1M filter_hom=[0,0]

#link(SRMAR.p2, OFI.p1)
#link(OFI.p3, OMC)

# (as built parameters: D1300507-v1)
s sSRM_OFI SRMAR.p2 OFI.p1 L=0.7278
s sOFI_OM1 OFI.p3 OM1.p1 L=2.9339

bs OM1 R=(1-(0.0008+&Mloss)) T=800u L=&Mloss alpha=2.251 Rc=[4.6, 4.6] # T is set for high power; Loss is a guess
s sOM1_OM2 OM1.p2 OM2.p1 L=1.395
bs OM2 R=(1-(1e-05+&Mloss)) T=10u L=&Mloss alpha=4.399 Rc=[1.7058, 1.7058] # T is a guess
s sOM2_OM3 OM2.p2 OM3.p1 L=0.631
bs OM3 R=(1-(1e-05+&Mloss)) T=10u L=&Mloss alpha=30.037 # T is a guess
s sOM3_OMC OM3.p2 OMC_IC.p1 L=0.2034

bs OMC_IC R=0.99239 T=0.0076 L=10u alpha=2.7609 
s lIC_OC OMC_IC.p3 OMC_OC.p1 L=0.2815 
bs OMC_OC R=0.99249 T=0.0075 L=10u alpha=4.004
s lOC_CM1 OMC_OC.p2 OMC_CM1.p1 L=0.2842
bs OMC_CM1 R=0.999954 T=36u L=10u alpha=4.004 Rc=[2.57321, 2.57321]
s lCM1_CM2 OMC_CM1.p2 OMC_CM2.p1 L=0.2815
bs OMC_CM2 R=0.9999541 T=35.9u L=10u alpha=4.004 Rc=[2.57369, 2.57369]
s lCM2_IC OMC_CM2.p2 OMC_IC.p4 L=0.2842
###############################################################################
### Length sensing and control
###############################################################################
dof XARM ETMX.dofs.z 1 
dof YARM ETMY.dofs.z 1 
dof CARM ETMX.dofs.z 1 ETMY.dofs.z 1    
dof DARM ETMX.dofs.z 1 ETMY.dofs.z -1 DC=0.0010350716735432314    
dof PRCL PRM.dofs.z 1  
dof SRCL SRM.dofs.z 1   DC=89.99999999999999
dof MICH BS.dofs.z 1  
dof MICH2 ITMY.dofs.z 1 ETMY.dofs.z 1 ITMX.dofs.z -1 ETMX.dofs.z -1        
dof STRAIN LX.dofs.h 1 LY.dofs.h -1    
dof FRQ L0.dofs.frq 1 
dof RIN L0.dofs.amp 1 

readout_rf REFL9 PRMAR.p1.o f=&f1 phase=5.276114879012382 output_detectors=true
readout_rf REFL18 PRMAR.p1.o f=(3*&f1)
readout_rf REFL45 PRMAR.p1.o f=(5*&f1)
readout_rf POP9  PR2.p3.o   f=&f1 phase=-169.13734272154758 output_detectors=true
readout_rf POP45 PR2.p3.o   f=&f2 phase=177.83524272452797 output_detectors=true
readout_rf AS45  SRMAR.p2.o f=&f2 phase=-6.146945800670171 output_detectors=true
readout_dc AS    OMC_OC.p3.o output_detectors=true

lock CARM_lock REFL9_I CARM.DC -0.00010310899995708841 1e-6
lock MICH_lock POP45_Q MICH.DC 16.69737563119658 1e-6
lock PRCL_lock POP9_I PRCL.DC -2.923409080736336 1e-6
lock SRCL_lock POP45_I SRCL.DC -43.220106415336616 1e-6
lock DARM_rf_lock AS45_I DARM.DC -0.00339832744582408 1e-6 disabled=true
lock DARM_dc_lock AS_DC DARM.DC -0.01 1e-6  disabled=false offset=20m
###############################################################################
### DC power measurements
###############################################################################
pd Px ETMX.p1.i
pd Py ETMY.p1.i
pd Pprc PRM.p2.o
pd Psrc SRM.p1.i
pd Prefl PRMAR.p1.o
pd Pas OMC_OC.p3.o

# Items below could not be matched to original script, or were not present when the model was originally parsed.
space L0_p1__mod1_p1 portA=L0.p1 portB=mod1.p1
space mod1_p2__mod2_p1 portA=mod1.p2 portB=mod2.p1
#space sqz_p1__OFI_p2 portA=sqz.p1 portB=OFI.p2
modes(maxtem=0)
cav cavOMC OMC_OC.p1.o
#cav cavOMC OMC_IC.p3.o via=OMC_IC.p4.i

cav cavSRX SRM.p1.o via=ITMX.p1.i priority=1
cav cavSRY SRM.p1.o via=ITMY.p1.i priority=1
cav cavPRX PRM.p2.o via=ITMX.p1.i priority=2
cav cavPRY PRM.p2.o via=ITMY.p1.i priority=2
cav cavXARM ETMX.p1.o priority=3
cav cavYARM ETMY.p1.o priority=3
